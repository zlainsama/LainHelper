buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.12.1"
group= "lain.mods.helper"
archivesBaseName = "LainHelper"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.1-14.22.0.2444"
    runDir = "run"

    mappings = "snapshot_20170624"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from 'LICENSE.txt'

    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}

task jarCore(type: Jar) {
    manifest {
        attributes 'FMLCorePlugin': 'lain.mods.helper.asm.Plugin'
    }
    from(sourceSets.main.output) {
        include '**/asm/**'
    }
    classifier = 'core'
    includeEmptyDirs = false
}

task signCore(type: SignJar) {
    onlyIf {
        project.hasProperty('mcModsStore')
    }

    if (project.hasProperty('mcModsStore')) {
        keyStore = project.mcModsStore
        alias = project.mcModsAlias
        storePass = project.mcModsStorePass
        keyPass = project.mcModsKeyPass
        inputFile = "${buildDir}/libs/${archivesBaseName}-${version}-core.jar"
        outputFile = "${buildDir}/libs/${archivesBaseName}-${version}-core.jar"
    }
}

task repackJar(type: Jar, dependsOn: signCore) {
    manifest {
        attributes 'ContainedDeps': "${archivesBaseName}-${version}-core.jar"
    }
    from(sourceSets.main.output) {
        exclude '**/asm/**'
    }
    from("${buildDir}/libs") {
        include "*-${version}-core.jar"
    }
}

reobf {
    jarCore {}
    repackJar {}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        project.hasProperty('mcModsStore')
    }

    if (project.hasProperty('mcModsStore')) {
        keyStore = project.mcModsStore
        alias = project.mcModsAlias
        storePass = project.mcModsStorePass
        keyPass = project.mcModsKeyPass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar
